---
title: How to Create Windows Inno Setup Installer for XULRunner Application
slug: >-
  Archive/Mozilla/XULRunner/How_to_Create_Windows_Inno_Setup_Installer_for_XULRunner_Application
tags:
  - XUL
  - XULRunner
translation_of: >-
  Archive/Mozilla/XULRunner/Creating_a_Windows_Inno_Setup_installer_for_XULRunner_applications
---
<p>
XULRunner アプリケーションでは現在インストーラを作る仕組みがまだありません。しかし、<a class="external" href="http://www.jrsoftware.org/isinfo.php">Inno Setup</a>を利用すれば簡単にWindows 用インストーラを作成する事ができます。
このドキュメントでは <a href="ja/Getting_started_with_XULRunner">Getting_started_with_XULRunner</a> の My App をサンプルを使ってWindowsインストーラを作成する手順を紹介します。
</p>
<h3 id="Step1._Install_Inno_Setup" name="Step1._Install_Inno_Setup"> Step1. Install Inno Setup </h3>
<p><a class="external" href="http://www.jrsoftware.org/isdl.php">Inno Setup Downloads</a> からInno Setup をダウンロードします。単純にインストーラのコンパイラを実行するだけであれば Stable Release でも良いのですが、 Script を編集するのであれば IStools(Script Editor) が含まれる QuickStart Pack がお勧めです。ただし、 IStools は <a class="external" href="http://www.jrsoftware.org/is3rdparty.php">Inno Setup Third-Party Files</a> から入手が可能です。
</p>
<h3 id="Step2._Create_Application" name="Step2._Create_Application"> Step2. Create Application </h3>
<p>XULRunner アプリケーションが単体で動作するように<a href="ja/XULRunner/Deploying_XULRunner_1.8">XULRunner:Deploying_XULRunner_1.8</a> の手順に沿って配置します。My App では以下のようになります。
</p>
<ul><li> &lt;tt&gt;<var>myapp</var>/&lt;/tt&gt;
<ul><li> &lt;tt&gt;application.ini&lt;/tt&gt;
</li><li> &lt;tt&gt;chrome/&lt;/tt&gt;
</li><li> &lt;tt&gt;default/&lt;/tt&gt;
</li><li> &lt;tt&gt;<var>myapp</var>.exe&lt;/tt&gt; (&lt;tt&gt;xulrunner-stub.exe&lt;/tt&gt; をコピーしてリネームしたものです)
</li><li> &lt;tt&gt;xulrunner/&lt;/tt&gt;
</li></ul>
</li></ul>
<p>アプリケーションの配置が終わったらこの段階でアプリケーションの動作テストをしておきます。アプリケーションが動かなかったらインストーラもうまく動作しないでしょう ;-)
</p>
<h3 id="Step3._Create_Inno_Setup_Script" name="Step3._Create_Inno_Setup_Script"> Step3. Create Inno Setup Script </h3>
<p>Inno Setup のスクリプト(iss)を作成します。
Inno Setup Script Wizard のみでは chrome などのディレクトリの配置場所が設定できないため、必ず iss ファイルを編集してください。
AppName, AppPublisher などは application.ini の記述と同じにすると良いでしょう。
</p><p>以下に My App でのサンプルを示します。
</p>
<pre>; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=My App
AppVerName=My App 1.0
AppPublisher=Mark Finkle
AppPublisherURL=http://developer.mozilla.org/en/docs/Getting_started_with_XULRunner
AppSupportURL=http://developer.mozilla.org/en/docs/Getting_started_with_XULRunner
AppUpdatesURL=http://developer.mozilla.org/en/docs/Getting_started_with_XULRunner
DefaultDirName={pf}\My App
DefaultGroupName=My App
AllowNoIcons=yes
OutputDir=..\build\output
OutputBaseFilename=myapp-1.0-win32
; SetupIconFile=
Compression=lzma
SolidCompression=yes

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Components]
Name: main; Description: My App; Types: full compact custom; Flags: fixed
Name: runtime; Description: XUL Runner Runtime; Types: full custom

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked
Name: quicklaunchicon; Description: {cm:CreateQuickLaunchIcon}; GroupDescription: {cm:AdditionalIcons}; Flags: unchecked

[Files]
Source: C:\develop\XULRunnerInstaller\myapp\myapp.exe; DestDir: {app}; Components: main; Flags: ignoreversion
Source: C:\develop\XULRunnerInstaller\myapp\application.ini; DestDir: {app}; Components: main; Flags: ignoreversion
Source: C:\develop\XULRunnerInstaller\myapp\chrome\*; Excludes: .svn; DestDir: {app}\chrome; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: C:\develop\XULRunnerInstaller\myapp\defaults\*; Excludes: .svn; DestDir: {app}\defaults; Components: main; Flags: ignoreversion recursesubdirs createallsubdirs
Source: C:\develop\XULRunnerInstaller\myapp\xulrunner\*; DestDir: {app}\xulrunner; Components: runtime; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: {group}\My App; Filename: {app}\myapp.exe
Name: {group}\{cm:UninstallProgram,XUL Explorer}; Filename: {uninstallexe}
Name: {userdesktop}\My App; Filename: {app}\myapp.exe; Tasks: desktopicon
Name: {userappdata}\Microsoft\Internet Explorer\Quick Launch\My App; Filename: {app}\myapp.exe; Tasks: quicklaunchicon

[Run]
Filename: {app}\myapp.exe; Description: {cm:LaunchProgram,My App}; OnlyBelowVersion: 0,6; Flags: nowait postinstall skipifsilent

</pre>
<p>また、今回のサンプルでは XULRunner が runtime として扱われていることに注意してください。
</p>
<h3 id="Step4._Building_Installer" name="Step4._Building_Installer"> Step4. Building Installer </h3>
<p>Inno Setup Compiler を起動して、Script ファイルを開いて Compile を実行します。Compile 後にはすぐにインストーラを実行してテストする事ができます。
インストーラ実行後にアプリケーションの動作テストをしてください。うまく起動したら成功です！
</p><p><a class="external" href="http://www.smellman.homelinux.org/mozilla/xul/myapp.iss">myapp.iss のサンプル</a> をダウンロードしてください。
</p>
<div class="originaldocinfo">
<h2 id="Original_Document_Information" name="Original_Document_Information"> Original Document Information </h2>
<ul><li> Author: Taro Matsuzawa
</li><li> Inno Setup Script Original Author: Mark Finkle
</li><li> Japanese version is original.
</li></ul>
</div>
